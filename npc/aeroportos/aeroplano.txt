//===== rAthena Script ======================================= 
//= The Airship System Script
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= The Airship System used in the official servers.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

//============================================================ 
//= Domestic Airship Route
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================ 

airplane,243,73,0	script	#AirshipWarp-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch_:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	45,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","Estamos indo para Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","Logo chegaremos em Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Einbroch. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","Atualmente estamos em Einbroch. O Aeroplano vai decolar em breve.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano est decolando. Nosso prximo destino  Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","Estamos indo para Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","Logo chegaremos em Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Lighthalzen. Tenha uma boa viagem.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","Atualmente estamos em Lighthalzen. O Aeroplano vai decolar em breve.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano est deixando o solo. Nosso prximo destino  Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","Estamos indo para Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","Logo chegaremos em Einbroch.",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Einbroch. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","Atualmente estamos em Einbroch. O Aeroplano vai decolar em breve.",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano est decolando. Nosso prximo destino  Juno.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","Estamos indo para Juno.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","Logo chegaremos em Juno.",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Juno. Tenha uma boa viagem.",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","Atualmente estamos em Juno. O Aeroplano sair em breve.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano est deixando o solo. Nosso prximo destino  Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","Estamos indo para Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","Logo chegaremos em Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Hugel. Tenha uma boa viagem.",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","Atualmente estamos em Hugel. O Aeroplano sair em breve.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano est deixando o solo. Nosso prximo destino  Juno.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","Estamos indo para Juno.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","Logo chegaremos em Juno.",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Juno. Tenha uma boa viagem.",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","Atualmente estamos em Juno. O Aeroplano sair em breve.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano est deixando o solo. Nosso prximo destino  Einbroch.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Sada#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Sada#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Sada#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Sada#airplane2b	857

airplane,100,69,3	script	Tripulao do Aeroplano#ein-1	852,{
	mes "[Tripulao do Aeroplano]";
	mes "Se j pousamos no";
	mes "seu destino e voc";
	mes "gostaria de sair do";
	mes "Aeroplano, por favor use as";
	mes "escadas  frente. Obrigado";
	mes "por seu apoio.";
	close;
}

airplane,64,94,1	script	Criana de Umbala#ein_p	787,{
	emotion ET_PROFUSELY_SWEAT;
	mes "[Criana]";
	if (event_umbala >= 3) {
		mes "Uau, me!";
		mes "V-veja isso!";
		mes "Ns estamos voando! N-ns estamos...";
		mes "Estamos conhecendo o cu!";
	}
	else {
		mes "Makumalagu!";
		mes "Saampa joojimbo";
		mes "kaku na jedi Solo.";
		mes "Bwahahahahahahaah!";
	}
	close;
}

airplane,66,93,3	script	Senhora de Umbala#ein_p	783,{
	emotion ET_THINK;
	mes "[Senhora]";
	if (event_umbala >= 3) {
		mes "Shush...";
		mes "Querido, comporte-se.";
		mes "No se anime tanto";
		mes "enquanto no sairmos";
		mes "deste lugar pblico!";
	}
	else {
		mes "Chooktu!";
		mes "Sacraup matii!";
		mes "Shaka gurftalfi";
		mes "huntiki manjoo!";
	}
	close;
}

airplane,71,91,7	script	Homem de Umbala#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[Chrmlim]";
		mes "Ei voc";
		mes "Pela sua aparncia,";
		mes "percebo que voc";
		mes "consegue me entender.";
		mes "^333333*Legal...!*^000000";
		next;
		emotion ET_HNG;
		mes "[Chrmlim]";
		mes "Tenho ajudado a equipe";
		mes "do Aeroplano, auxiliando";
		mes "seus Tripulantes que tm";
		mes "medo de altura os levanto para pular";
		mes "de bungee jumping em Umbala. Legal, em?";
		next;
		mes "[Chrmlim]";
		mes "Mas...";
		mes "Alguns deles no podem";
		mes "superar seus medos.";
		mes "Devem participar de";
		mes "algumas experincias, ^333333em Nifflheim^000000.";
	}
	else {
		mes "[Chrmlim]";
		mes "Bajoo ga";
		mes "Aeroplano Nukta.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion ET_HNG;
		mes "[Chrmlim]";
		mes "Shabala moow bajama";
		mes "Vamos para o aeroplano";
		mes "salu. Dama, kookoo na nu";
		mes "yukuta. Um, fashuku na ret!";
	}
	close;
}

airplane,250,58,2	script	Equipe do Aeroplano#airplane	67,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Equipe do Aeroplano]";
		mes "Bem Vindo";
		mes "ao Aeroplano.";
		mes "como posso ajudar?";
		next;
		select("Voc tem um passageiro chamado Thierry?");
		mes "[Equipe do Aeroplano]";
		mes "Me desculpe, mas no conheo nenhum dos passageiros por esse nome.";
		close;
	}
	// Fim da Quest de Hugel
	mes "[Assistente do Aeroplano]";
	mes "Bem-vind"+(Sex?"o":"a")+"";
	mes "ao Aeroplano.";
	mes "Como posso ajud-l"+(Sex?"o?":"a?")+"";
	next;
	switch (select("Usando o Aeroplano:Cabine do Capito:Instalaes:Cancelar")) {
	case 1:
		mes "[Assistente do Aeroplano]";
		mes "Quando voc ver uma mensagem";
		mes "anunciando que ns";
		mes "chegamos ao seu destino";
		mes "por favor, use uma das sadas";
		mes "localizadas ao Norte";
		mes "e ao Sul do aeroplano.";
		next;
		mes "[Assistente do Aeroplano]";
		mes "Se acontecer de voc perder";
		mes "sua parada, no se preocupe.";
		mes "O Aeroplano est constantemente";
		mes "na rota e voc ter";
		mes "outra chance de descer";
		mes "em seu destino.";
		close;
	case 2:
		mes "[Assistente do Aeroplano]";
		mes "A Cabine do Capito";
		mes "fica na parte frontal";
		mes "do Aeroplano. L, voc";
		mes "pode conhecer o capito e";
		mes "o piloto do Aeroplano.";
		close;
	case 3:
		mes "[Assistente do Aeroplano]";
		mes "O Aeroplano possui";
		mes "vrios mini-games para";
		mes "o entretenimento de todos";
		mes "os passageiros. Voc est";
		mes "convidad"+(Sex?"o":"a")+" a testar sua sorte";
		mes "nos Mini-Games do Aeroplano~";
		close;
	case 4:
		mes "[Assistente do Aeroplano]";
		mes "Bem, desejo-lhe uma boa";
		mes "estadia durante o vo em";
		mes "nosso Aeroplano. Obrigado";
		mes "e tenha um bom dia.";
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	834,{
	mes "[Zerta]";
	mes "Oh, ol Aventureiro.";
	mes "Estou atualmente em uma";
	mes "Jornada Santa, orando";
	mes "em propsito do continente";
	mes "de Rune-Midgard.";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	714,{
	mes "[Maelin]";
	mes "Humm, este avio vai para";
	mes "Lutie, no? Estou";
	mes "esperando h muito tempo,";
	mes "mas at agora nada de";
	mes "anncio sobre Lutie.";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	702,{
	mes "[Aanos]";
	mes "Oh wooow~";
	mes "O cu tem uma aparncia";
	mes "to diferente e";
	mes "adorvel, visto daqui de cima!";
	close;
}

airplane,221,158,2	script	Pilot#airplane	852,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Piloto]";
		mes "Espero que o drink esteja fresco e saboroso.";
		mes "Bebidas so a minha vida! Beber me d energia!";
		mes "No vivo sem bebidas!";
		next;
		mes "[Piloto]";
		mes "Mas! Dirigir sob influncia da bebida no  bom.";
		mes "Porm! Preciso estar bebendo mais e mais!";
		emotion ET_CRY;
		next;
		select("Voc conhece um passageiro chamado Thierry?");
		mes "[Piloto]";
		mes "Este uniforme";
		mes " realmente bonito, mas";
		mes "no gosto de us-lo";
		mes "enquanto estou no Aeroplano.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Ningum nunca realmente";
		mes "entrou neste quarto.";
		mes "E como sou o capito.^FFFFFF ^000000 Eu poderia simplesmente ficar de cuecas.";
		next;
		emotion ET_HUK;
		mes "[Piloto]";
		mes "Ah!? O que  isto!";
		next;
		mes "- ...Ele no est te ouvindo. -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[Piloto]";
		mes "Faz bastante tempo";
		mes "desde que eu";
		mes "experimentei um bom e gelado";
		mes "lcool. Mas a";
		mes "profisso requer que eu";
		mes "esteja sempre sbrio!";
		next;
		mes "[Piloto]";
		mes "Sempre beba com responsabilidade!";
		mes "Ainda sim, no consigo me lembrar";
		mes "da ltima vez que tive umas frias de verdade";
		mes "ou at mesmo um dia de folga, sim";
		mes "uma bebida, algumas batatas fritas";
		mes "um pouco de TV e tudo ficar timo.";
		emotion ET_CRY;
		close;
	case 2:
		mes "[Piloto]";
		mes "Cara, o clima";
		mes "est timo hoje.";
		mes "Brilho, o cu est aberto";
		mes "com uma boa visibilidade";
		mes "e segurana, um vo despreocupado.";
		close;
	case 3:
		mes "[Piloto]";
		mes "Voc sabe, seu capito ";
		mes "um homem respeitado. Ele e";
		mes "seu irmo so atualmente";
		mes "os mais conhecidos na indstria area.";
		mes "Quem dira que nos tornariamos";
		mes "capites de vo to bons?";
		next;
		mes "[Piloto]";
		mes "Apenas entre ns,";
		mes "Tenho que dizer a voc,";
		mes "o Papai Noel foi para algum";
		mes "lugar com renas e elfos";
		mes "que trabalham para ele.";
		mes "Aquele cara  um gnio!";
		close;
	default:
		mes "[Piloto]";
		mes "Voc sabe, toda essa";
		mes "coisa de pilotar no ar,";
		mes " algo bastante recente, voc sabe no ?";
		mes "Sim, temos um Aeroplano";
		mes "voando a uma incrvel velocidade.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Ainda assim, h sempre algo srio,";
		mes "realmente acontecendo.";
		mes "Quer dizer, estvamos";
		mes "treinando enquanto o Aeroplano";
		mes "estava sendo inventado.";
		mes "No  uma loucura?!";
		close;
	}
}

airplane,50,66,5	script	Apple Merchant#airplane	86,{
	mes "[Fruitz]";
	mes "Bem vindo ao Fruitz's";
	mes "onde voc pode";
	mes "comprar mas ou tritur-las";
	mes "para fazer suco de ma.";
	next;
	switch (select("Comprar Mas.:Fazer Suco de Ma.:Por que voc est aqui?:Cancelar.")) {
	case 1:
		mes "[Fruitz]";
		mes "Por favor me diga a quantidade";
		mes "de Mas que voc deseja";
		mes "comprar. Cada Ma custa 15 zeny";
		mes "e voc pode comprar no mximo";
		mes "500 por vez.",
		mes "Digite '0' para cancelar sua compra.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Fruitz]";
				mes "Obrigado por parar";
				mes "na minha loja. Adeus!";
				mes "Venha a qualquer hora quando";
				mes "voc sentir vontade de ter uma";
				mes "Ma para fazer um lanche.";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Fruitz]";
				mes "Voc digitou um nmero";
				mes "maior do que o valor mximo";
				mes "de 500. Por favor digite";
				mes "novamente o nmero de mas";
				mes "que voc deseja comprar.";
				next;
			}
			else {
				mes "[Fruitz]";
				mes "Um total de ^FF0000" + .@input + "^000000 Mas";
				mes "que ir custar a voc ^FF0000" + .@pay + " Zeny^000000.";
				mes "Voc gostaria de continuar?";
				next;
				if (select("Sim:No") == 2) {
					mes "[Fruitz]";
					mes "Obrigado por parar";
					mes "na minha loja. At mais!";
					mes "Venha a qualquer hora quando";
					mes "voc sentir vontade";
					mes "fazer um lanche";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Fruitz]";
			mes "Sinto muito, mas voc no";
			mes "tem dinheiro suficiente para";
			mes "comprar muitas Mas.";
			mes "Por favor, verifique o seu zeny ou";
			mes "compre poucas Mas.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Fruitz]";
			mes "Hmmm, eu no acho que";
			mes "que voc tenha espao suficiente no";
			mes "seu inventrio para levar";
			mes "muitas Mas. Por que voc no libera algum espao no seu inventrio?";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Fruitz]";
			mes "Obrigado pela visita";
			mes "na minha loja. Espero que voc goste";
			mes "do aroma dessas Mas~!";
			close;
		}
	case 2:
		mes "[Fruitz]";
		mes "Tudo bem, precisarei de";
		mes "^FF00003 Mas e 1 Garrafa Vazia^000000";
		mes "para criar 1 Suco de Ma para voc";
		mes "Gostaria de continuar?";
		next;
		switch (select("Sim:No")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Fruitz]";
				mes "Me desculpe,mas voc no possui";
				mes "os materiais suficientes para";
				mes "criar uma Garrafa de Suco de Ma.";
				mes "Lembre-se, irei precisar de 3 Mas";
				mes "e 1 Garrafa Vazia.";
				close;
			}
			else {
				mes "[Fruitz]";
				mes "Obrigado,";
				mes "por favor";
				mes "espere um momento.";
				next;
				mes "^3355FF*Grind grind*";
				mes "*Grind grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Ma
				delitem 713,1; //Garrafa Vazia
				getitem 531,1; //Suco de Ma
				mes "[Fruitz]";
				mes "Aqui est";
				mes "Espero que voc goste!";
				mes "Por favor fique a vontade";
				mes "para comprar Mas";
				mes "ou fazer Sucos de Ma quando";
				mes "voc precisar, aventureiro";
				close;
			}
		case 2:
			mes "[Fruitz]";
			mes "Obrigado por parar";
			mes "na minha loja. Adeus!";
			mes "Venha a qualquer hora quando";
			mes "voc sentir vontade de ter uma";
			mes "Ma para fazer um lanche ~";
			close;
		}
	case 3:
		mes "[Fruitz]";
		mes "Eu costumava ser um andarilho";
		mes "vagabundo, at que um dia,";
		mes "Tirei uma soneca e algo";
		mes "atingiu minha cabea e";
		mes "acordei do meu sono tranquilo.";
		next;
		mes "[Fruitz]";
		mes "Acontece que eu estava";
		mes "dormindo embaixo de uma Macieira";
		mes "e foi quando uma ma caiu";
		mes "e atingiu minha cabea.";
		mes "Eu estava morrendo de fome";
		mes "e estava prestes a comer aquela Ma...";
		next;
		mes "[Fruitz]";
		mes "Mas de repente, Kain, meu antigo";
		mes "amigo desde os tempos de minerao,";
		mes "me pediu para ajud-lo aqui";
		mes "no Aeroplano. Ento eu aceitei, e";
		mes "foi l que encontrei pessoas";
		mes "jogando um Jogo de Dados.";
		next;
		mes "[Fruitz]";
		mes "Estava entediado e curioso";
		mes "e acabei apostando algumas simples";
		mes "Mas no jogo dos dados.";
		mes "Mas por alguma razo,";
		mes "eu tive uma incrvel sorte.";
		mes "Uma ma se multiplicou em duas... ";
		next;
		mes "[Fruitz]";
		mes "Duas tornaram-se quatro";
		mes "e antes que eu percebesse, eu j tinha feito";
		mes "um mercado de Mas!";
		mes "Ganhei tantas mas, e";
		mes "acabei comeando meu prprio negcio";
		mes "aqui no Aeroplano. Estranho, no acha?";
		next;
		mes "[Fruitz]";
		mes "Ento As mas so boas";
		mes "para voc. Elas foram";
		mes "certamente muito boas";
		mes "para mim. Hahahahaah~!";
		close;
	case 4:
		mes "[Fruitz]";
		mes "Obrigado por";
		mes "usar minha loja";
		mes "Adeus~";
		close;
	}
}

//============================================================ 
//= Rota internacional do Aeroplano
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AirshipWarp-3	45,1,1,{
	end;
	
OnTouch_:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	45,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","Estamos indo para Izlude.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","Logo chegaremos em Izlude.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation2,1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo(a) a Izlude. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","Estamos atualmente em Izlude. O Aeroplano vai decolar em breve.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano est decolando. Nosso prximo destino  Juno.",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","Estamos indo para Juno.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","Logo chegaremos em Juno.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation2,2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo(a) a Juno. Tenha uma boa viagem.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","Estamos atualmente em Juno. O Aeroplano sair em breve.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano est deixando o solo. Nosso prximo destino  Rachel.",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","Estamos indo para Rachel.",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","Logo chegaremos em Rachel.",bc_map,"0xFF8200";
	end;
OnTimer220000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo(a) a Rachel. Tenha uma boa viagem.",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","Estamos atualmente em Rachel. O Aeroplano vai decolar em breve.",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano est decolando. Nosso prximo destino  Izlude.",bc_map,"0x00ff00";
	stopnpctimer;
	set .moninv, .moninv + 1;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		set .moninv, 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Sada#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Sada#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	Sada#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	Sada#airplane_012b	857

airplane_01,250,58,2	script	Equipe do Aeroplano#airplane01	67,{
	mes "[Equipe do Aeroplano]";
	mes "Bem vindo";
	mes "ao Aeroplano.";
	mes "como posso ajudar?";
	next;
	switch (select("Usando o Aeroplano:Cabine do Capito:Instalaes:Cancelar")) {
	case 1:
		mes "[Assistente do Aeroplano]";
		mes "Quando voc ver o aviso";
		mes "anunciando que chegamos";
		mes "no seu destino desejado,";
		mes "por favor, use uma das sadas";
		mes "localizadas nas partes";
		mes "norte e sul do Aeroplano.";
		next;
		mes "[Assistente do Aeroplano]";
		mes "Se acontecer de voc perder";
		mes "sua parada, no se preocupe.";
		mes "O Aeroplano est constantemente";
		mes "na rota e voc ter";
		mes "outra chance para chegar";
		mes "ao seu destino pretendido.";
		close;
	case 2:
		mes "[Assistente do Aeroplano]";
		mes "A Cabine do Capito";
		mes "fica na parte frontal";
		mes "do Aeroplano. L, voc";
		mes "pode conhecer o capito e";
		mes "o piloto do Aeroplano.";
		close;
	case 3:
		mes "[Assistente do Aeroplano]";
		mes "O Aeroplano possui";
		mes "vrios mini-games para";
		mes "o entretenimento de todos";
		mes "os passageiros. Voc est";
		mes "convidad"+(Sex?"o":"a")+" a testar sua sorte";
		mes "nos Mini-Games do Aeroplano~";
		close;
	case 4:
		mes "[Assistente do Aeroplano]";
		mes "Bem, desejo-lhe uma boa";
		mes "estadia durante o vo em";
		mes "nosso Aeroplano. Obrigado";
		mes "e tenha um bom dia.";
		close;
	}
}

airplane_01,50,66,5	script	Mercador de Mas#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto !! -";
		mes "- Atualmente voc est carregando -";
		mes "- muitos itens com voc. -";
		mes "- Por favor, tente novamente -";
		mes "- depois de perder algum peso. -";
		close;
	}
	mes "[Meltz]";
	mes "Bem vindo ao Meltz's";
	mes "onde voc pode";
	mes "comprar mas ou tritur-las";
	mes "para fazer Suco de Ma.";
	next;
	switch (select("Comprar Mas.:Fazer Suco de Ma.:Cancelar.")) {
	case 1:
		mes "[Meltz]";
		mes "Por favor digite a quantidade";
		mes "de mas que voc deseja";
		mes "comprar. Cada Ma custa 15 zenys";
		mes "e voc pode comprar um mximo";
		mes "de 500 de uma vez. Por favor digite";
		mes "'0' para cancelar seu pedido.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "Obrigado por parar";
				mes "na minha loja. Adeus!";
				mes "Venha a qualquer hora quando";
				mes "voc sentir vontade de ter uma";
				mes "Ma para fazer um lanche";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Meltz]";
				mes "Voc digitou um nmero";
				mes "maior do que o valor mximo";
				mes "de 500. Por favor digite";
				mes "novamente o nmero de mas";
				mes "que voc deseja comprar.";
				next;
			}
			else {
				mes "[Meltz]";
				mes "Um total de ^FF0000" + .@input + "^000000 Mas";
				mes "que ir custar a voc ^FF0000" + .@pay + " Zeny^000000.";
				mes "Voc gostaria de continuar?";
				next;
				if (select("Yes:No") == 2) {
					mes "[Meltz]";
					mes "Obrigado por parar";
					mes "na minha loja. At mais!";
					mes "Venha a qualquer hora quando";
					mes "voc sentir vontade de";
					mes "fazer um lanche";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "Sinto muito, voc no tem";
			mes "dinheiro suficiente.";
			mes "Por favor, verifique quanto ainda tem ou";
			mes "compre poucas mas.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "Hmm, acho que voc no tem";
			mes "espao suficiente para transportar";
			mes "essa qauntidade enorme de mas.";
			mes "Voc pode liberar";
			mes "espao em seu inventrio";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "Obrigado pela visita";
			mes "na minha loja. Espero que voc goste";
			mes "do aroma dessas Mas!";
			close;
		}
	case 2:
		mes "[Meltz]";
		mes "Tudo bem, precisarei de";
		mes "^FF00003 Mas e 1 Garrafa Vazia^000000";
		mes "para criar 1 Suco de Ma para voc";
		mes "Gostaria de continuar?";
		next;
		switch (select("SIm:No")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Meltz]";
				mes "Me desculpe,mas voc no possui";
				mes "os materiais suficientes para";
				mes "criar uma Garrafa de Suco de Ma.";
				mes "Lembre-se, irei precisar de 3 Mas";
				mes "e 1 Garrafa Vazia.";
				close;
			}
			else {
				mes "[Meltz]";
				mes "Thank you, please wait.";
				next;
				mes "^3355FF*Grind* *Grind*";
				mes "*Grind* *Grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Ma
				delitem 713,1; //Garrafa Vazia
				getitem 531,1; //Suco de Ma
				mes "[Meltz]";
				mes "Aqui est";
				mes "Por favor, venha novamente.";
				close;
			}
		case 2:
			mes "[Meltz]";
			mes "Obrigado por parar";
			mes "na minha loja. Adeus!";
			mes "Venha a qualquer hora quando";
			mes "voc sentir vontade de ter uma";
			mes "Ma para fazer um lanche ~";
			close;
		}
	case 3:
		mes "[Meltz]";
		mes "Obrigado por parar";
		mes "na minha loja. Adeus!";
		mes "Venha a qualquer hora quando";
		mes "voc sentir vontade de ter uma";
		mes "Ma para fazer um lanche ~";
		close;
	}
}

airplane_01,221,158,2	script	Piloto#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[Piloto]";
		mes "Longitude, 131 graus leste.";
		mes "Latitude, 37 graus norte.";
		mes "Estamos no curso certo, capito.";
		close;
	case 2:
		mes "[Piloto]";
		mes "Parece ser realmente um";
		mes "dia nublado. Sempre  difcil";
		mes "navegar quando os cus";
		mes "no esto limpos. Acho que ns vamos";
		mes "precisar ampliar o radar.";
		close;
	case 3:
		mes "[Piloto]";
		mes "O Capito  um bom";
		mes "homem e eu no consigo pensar em";
		mes "uma pessoa mais fina para comandar";
		mes "este Aeroplano. Ainda assim, ele  muito";
		mes "duro, um verdadeiro motorista servo.";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000Ei...!^000000";
		mes "^ff0000Menos bate-papo^000000";
		mes "^ff0000e mais pilotagem!^000000";
		next;
		mes "[Piloto]";
		mes "C-certo, senhor!";
		mes "(Viu o que eu quero dizer?)";
		close;
	default:
		mes "[Piloto]";
		mes "Este uniforme ";
		mes "bem elegante, mas";
		mes " muito chamativo para";
		mes "usar pelo Aeroplano.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Realmente ningum";
		mes "nunca entra neste quarto.";
		mes "E o capito  uma rena.";
		mes "Eu podia tirar a minha cueca.";
		next;
		emotion ET_HUK;
		mes "[Piloto]";
		mes "Oh...! Ol!";
		mes "E-e-est aproveitando o vo?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[Dianne]";
	mes " to estranho!";
	mes "Eu venho visitar";
	mes "o Capito do Aeroplano";
	mes "e tudo que vejo  esse";
	mes "alce estranho. Oh!";
	mes "No esperava por essa...";
	close;

OnTouch_:
	emotion ET_CRY;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[Mendel]";
	mes "Como eu esperava, as";
	mes "comidas do vo so";
	mes "de qualidade 3 estrelas no melhor.";
	mes "*Harrrumph* Eu realmente deveria";
	mes "ter trazido meu cozinheiro";
	mes "Eu desfrutaria de uma verdadeira refeio.";
	close;
}

airplane_01,71,31,2	script	Swordsman Shimizu#air_01	106,{
	mes "[Espadachim Shimizu]";
	mes "Finalmente, depois de cinco";
	mes "anos esperando...";
	mes "Terei minha vingana!";
	next;
	mes "[Espadachim Shimizu]";
	mes "Eu s...";
	mes "Tenho que me certificar";
	mes "de que no perderei minha";
	mes "parada. Mas logo, muito breve,";
	mes "terei minha vingana.";
	close;
}

//============================================================ 
//= Desafio de Escrita
//============================================================ 
airplane_01,32,61,4	script	Nils#ein	49,1,1,{
	mes "[Nils]";
	mes "Bem vindo ao";
	mes "^ff0000Desafio de Digitao do LivedRO^000000.";
	mes "Voc gostaria de jogar";
	mes "um jogo de digitao rpida?";
	next;
	switch (select("Jogar ^ff0000Desafio de Escrita LivedRO^000000:Informaes:Ver recordes:Cancelar")) {
	case 1:
		mes "[Nils]";
		mes "Ok, ns temos";
		mes "um novo desafiante!";
		mes "Digite o seguinte";
		mes "texto o mais rpido";
		mes "que puder sem cometer";
		mes "nenhum erro! Vamos comear~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcEu sou o rei dos esquisitos! Agora^000000",
					"^3cbcbcVoc no me deu escolha. Eu aposto que^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcvoc conhece meu verdadeiro poder. Obey~!^000000",
					"^3cbcbchora de revelar meu segredo...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcDEUS-POING. EU NUNCA PERCO!^000000",
					"",
					"",
					"^3cbcbcCOBO LIGADO^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"Eu sou o rei dos esquisitos! Agora voc conhece meu verdadeiro poder. Obey~!",
					"Voc no me deu escolha. Eu aposto que  hora de revelar meu poder...";
		setarray .@line2_1$[0],	"^3cbcbciMpertuBVEl AmVel SeCreTo E^000000",
					"^ff1493VeLoCiDaDe Da LuZ ViRaR a EsQuErDa^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493acreditar na sorte do amor suar careta correr^000000",
					"^800080abriR, Abrir!abr1r.abrirAbrirAbr1rAbRir4brir^000000",
					"^3cbcbclIberDadE xtAse JoRnAlIsMo ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboLiChe cOnTrA O kArmA JoVem^000000",
					"^ff1493CeRtO QuEiMe VeRdAdEiRo GaNgSteR bEijo^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`abrir'4Brir abrir? abrir!111ABRAMESMO^000000",
					"^3cbcbcDesCobrir dOr de CaBea RaiosLunArES jUsTiA^000000";
		setarray .@word2$[0],	"iMpertuBVEl AmvEl SeGrEdOs E boLiCHE cONtra o kArmA dos JoVeMs",
					"VeLoCiDaDe Da LuZ ViRaR a EsQuErDa QuEiMe VeRdAdEiRo GaNgSteR bEijo",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"acreditar na sorte do amor suar careta correr conto-de-fadas rodimus optimus bumblebee",
					"abriR, Abrir!abr1r.abrirAbrirAbr1rAbRir4brir`abrir'4Brir abrir? abrir!111ABRAMESMO",
					"lIberDadE xtAse JoRnAlIsMo AxIla DesCobrir dOr de CaBea RaiosLunArES jUsTiA";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		set .@wordtest, rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save1$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save2$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Nils]";
			mes "Seu recorde  ^ff0000" + .@total_time + " segundos^000000 e";
			mes "o total de letras so " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Nils]";
				mes "Humm, no  possvel";
				mes "humanamente falando, a menos que";
				mes "copie e cole toda a";
				mes "sentena. Por favor, jogue novamente.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Nils]";
			mes "O recorde anterior foi";
				mes "feito por ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "com o total de ^0000ff" + $050320_ein_typing + "^000000 letras.";
				mes "Porm, ^ff0000" + strcharinfo(0) + "^000000,";
				mes "voc o novo top record";
				mes "desta vez. Parabns!";
				set $050320_minus1_typing$, strcharinfo(0);
				set $050320_ein_typing, .@tasoo;
				close;
			}
			else {
				mes "[Nils]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "esse  o recorde";
				mes "atual conquistado com";
				mes "um total de ^0000ff" + $050320_ein_typing + "^000000";
				mes "caracteres. Tente bater";
				mes "esse record na prxima vez~";
				close;
			}
		}
		else {
			mes "[Nils]";
			mes "Oooh...";
			mes "Me desculpe, mas";
			mes "voc digitou o";
			mes "texto incorreto...";
			close;
		}
	case 2:
		mes "[Nils]";
		mes "O ^ff0000Desafio de Escrita LivedRO^000000";
		mes " um jogo onde voc deve digitar";
		mes "o texto exibido o mais rpido que voc";
		mes "puder. O nome do jogador que possuir o";
		mes "recorde ser gravado nos registros. Se voc";
		mes "procura fama, essa  sua chance!";
		next;
		mes "[Nils]";
		mes "Eu apenas gostaria de";
		mes "dizer que voc deve digitar";
		mes "todo o texto em uma simples";
		mes "linha que ser apresentada a voc.";
		mes "Ento no pressione uma tecla, apenas clique em 'OK.'";
		close;
	case 3:
		mes "[Nils]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "esse  o recorde";
		mes "atual conquistado com";
		mes "um total de ^0000ff" + $050320_ein_typing + "^000000";
		mes "caracteres. Tente bater";
		mes "esse record na prxima vez";
		close;
	case 4:
		mes "[Nils]";
		mes "Sinta-se livre para voltar quando quiser e jogar o";
		mes "^ff0000Desafio de Escrita LivedRO^000000";
		mes "At logo";
		close;
	}
}

//============================================================ 
//= Jogo da Ma
//============================================================ 
airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "Oi, eu sou Clarice";
	mes "Voc gostaria de";
	mes "apostar algumas mas";
	mes "no amigvel jogo dos Dados?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

function	script	applegamble	{
	.@npc_name$ = getarg(0);
	switch (select("Jogar:Regras do Jogo:Cancelar")) {
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "Estou pronta para";
		mes "o jogo dos dados";
		mes "e voc, est a vontade para jogar";
		mes "basta falar comigo";
		mes "se sentir vontade de jogar, Ok??";
		close;
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "As regras do Jogo dos Dados";
		mes "so bastante simples. Primeiro, voc";
		mes "deve saber que as apostas so Mas.";
		mes "Voc pode apostar no mximo 50";
		mes "Mas por vez. Para que tudo ocorra";
		mes "bem, eu aceito apenas Mas.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Ei, se todo esse zeny";
		mes "est fazendo um buraco no seu";
		mes "bolso, v ao Fruitz";
		mes "e poder comprar quantas";
		mes "mas que voc quiser";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Agora, ns iremos comear rolando";
		mes "dois dados de 6 lados.";
		mes "Enquanto for seu turno, voc poder";
		mes "rolar dois dados de 6 lados. Aps";
		mes "isso, ns teremos a opo";
		mes "de rolar um terceiro dado.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Agora aqui  a parte importante.";
		mes "Se ao total nos dados voc atingir 12,";
		mes "voc ir a falncia";
		mes "e ir perder. Caso contrrio, a";
		mes "a pessoa com a maior pontuao";
		mes "ir vencer. O que acha?";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Agora, primeiro voc deve";
		mes "decidir se quer jogar o";
		mes "terceiro dado ou no. Ento,";
		mes "dependendo do seu resultado, eu irei";
		mes "rolar meu terceiro dado... Ou talvez no.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Enquanto voc ganhar, voc ir";
		mes "receber o dobro de Mas";
		mes "que voc apostou.";
		mes "Mas se por acaso voc empatar, voc ir";
		mes "receber de volta apenas as Mas";
		mes "que voc apostou. Justo, certo?";
		close;
	case 1:
		break;
	}
	mes "["+ .@npc_name$ +"]";
	mes "Ah, Ento voc quer jogar comigo?";
	mes "timo! Quantas Mas";
	mes "voc deseja apostar?";
	mes "Lembre-se, seu limite so 50 Mas.";
	mes "Se voc deseja cancelar, por favor digite '0'.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "Mudou de idia?";
			mes "Eu entendo. Bem ento,";
			mes "espero que possamos jogar depois.";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+ .@npc_name$ +"]";
			mes "Voc no pode apostar mais do que";
			mes "50 Mas. Lembre-se,";
			mes "precisamos manter as apostas";
			mes "razoveis. Por favor digite";
			mes "um valor no superior a 50.";
			next;
			continue;
		}
		mes "["+ .@npc_name$ +"]";
		mes "Ento voc estar";
		mes "apostando ^FF0000"+.@amount+"^000000 Mas.";
		mes "Certo?";
		next;
		if (select("Yes:No") == 2) {
			mes "["+ .@npc_name$ +"]";
			mes "Hum, cometi um erro?";
			mes "Certo, por favor digite";
			mes "o nmero de Mas que";
			mes "voc deseja apostar.";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "Me desculpe, mas voc";
			mes "no parecem ter";
			mes "Mas suficientes para essa";
			mes "aposta... Voc no pode apostar";
			mes "se no tem a aposta, voc sabe.";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+ .@npc_name$ +"]";
		mes "Good!";
		mes "Now we can start";
		mes "this game! I'll roll first~";
		break;
	}
	mes "^3355FF*Rolling and rumbling*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+ .@npc_name$ +"]";
	mes "Eu tenho um ^0000FF" + .@table1 + "^000000 e um ^0000FF" + .@table2 + "^000000.";
	mes "Isso  um total de ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, agora  sua vez.";
	next;
	select("Lanar Dado.");
	mes "^3355FF*Rolando e caindo*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+ .@npc_name$ +"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, voc tem ^FF0000" + .@player1 + "^000000 e ^FF0000" + .@player2 + "^000000. O total  ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+ .@npc_name$ +"]";
	if(.@playersub == .@tablesub) {
		mes "Atualmente meu total  ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total  ^FF0000" + .@playersub + "^000000. Estamos empatados, gostaria de lanar o dado novamente?";
	} else if (.@playersub > .@tablesub) {
		mes "Atualmente meu total  ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total  ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, atualmente voc est ganhando o jogo, gostaria de lanar o dado novamente?";
	} else if(.@tablesub > .@playersub) {
		mes "Atualmente meu total  ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total  ^FF0000" + .@playersub + "^000000. Estou vencendo o jogo. Gostaria de lanar o dado novamente?";
	}
	next;
	switch (select("Lanar Dado.:Cancelar.")) {
	case 1:
		mes "^3355FF*Rolando e Caindo*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+ .@npc_name$ +"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, voc possui ^FF0000" + .@player3 + "^000000 e o total agora  ^FF0000" + .@playertotal + "^000000. Voc perdeu o jogo. Me desculpe mas, por favor tente novamente.";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, voc possui ^FF0000" + .@player3 + "^000000 e o total agora  ^FF0000" + .@playertotal + "^000000. Mesmo que voc lance o dado novamente, seu total ainda ser menor do que o meu.  Voc perdeu o jogo. Sinto muito e por favor tente novamente.";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, voc possui ^FF0000" + .@player3 + "^000000 e o total agora  ^FF0000" + .@playertotal + "^000000. Eu no quero correr nenhum risco, vamos acabar este jogo num empate. Vamos jogar de novo outra hora~";
				close2;
				getitem 512,.@amount; //Ma
				end;
			}
			mes "Tudo bem.";
			mes "Deixe-me jogar os dados novamente.";
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. Now it is my turn.";
		}
		break;
	case 2:
		mes "["+ .@npc_name$ +"]";
		if (.@playersub > .@tablesub) {
			mes "Entendo, voc no quer correr risco de perder o jogo. Ok, deixe-me lanar os dados novamente.";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "Entendo, voc no quer correr risco de perder este jogo. Nem eu, vamos acabar este jogo em um empate. Vamos jogar novamente outra hora~";
				close2;
				getitem 512,.@amount; //Ma
				end;
			}
			mes "Tudo bem.";
			mes "Deixe-me jogar os dados novamente.";
		}
		else {
			mes "No faria mal tentar.";
			mes "Bem, eu venci dessa vez.";
			mes "Sinto muito, vamos tentar jogar";
			mes "novamente algum dia.";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolando e Caindo*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+ .@npc_name$ +"]";
	if (.@tabletotal > 12) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora  ^0000FF" + .@tabletotal + "^000000. Perdi o jogo por que meu total excedeu 12. Deixe-me dar-lhe minhas mas. Parabns, foi um grande jogo.";
		close2;
		getitem 512,.@giveapple; //Ma
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora  ^0000FF" + .@tabletotal + "^000000. Com o total de ^FF0000" + .@playertotal + "^000000 voc ganhou este jogo, ^FF0000" + strcharinfo(0) + "^000000. Deixe-me dar-lhe minhas mas. Foi um grande jogo e espero que possamos jogar novamente em outro momento.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora  ^0000FF" + .@tabletotal + "^000000. Com o total de ^FF0000" + .@playertotal + "^000000 este jogo acabou em empate, ^FF0000" + strcharinfo(0) + "^000000. Deixe-me dar-lhe suas mas de volta. Foi um grande jogo e espero que possamos jogar novamente em outro momento.";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora  ^0000FF" + .@tabletotal + "^000000. Com o total de ^FF0000" + .@playertotal + "^000000 voc perdeu o jogo, ^FF0000" + strcharinfo(0) + "^000000. Lamento, mas por favor, tente novamente.";
		close;
	}
}
