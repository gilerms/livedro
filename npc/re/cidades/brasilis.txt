//===== rAthena Script ======================================= 
//= Brasilis Town
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Brasilis Town Script
//===== Additional Comments: ================================= 
//= 1.0 First version. Transportation and Basic NPCs.
//= 1.1 Fixed Zeny not being removed for payment. [Kisuka]
//= 1.2 Updated dialog, added Ice Cream Maker. [L0ne_W0lf]
//= 1.3 Moved Alberta NPC to pre-re/re paths. [Euphy]
//= 1.4 Added VIP features. [Euphy]
//============================================================ 

// Brasilis Transportation :: brasilis_trans
//============================================================
/* Pre-Renewal coordinates: alberta,247,115,3 */
alberta,246,82,3	script	Tripulante#bra2	100,{
	mes "[Tripulante]";
	mes "Ei, você já ouviu falar de um lugar chamado Brasilis?";
	mes "É uma cidade tropical quente como o deserto, mas também chuvosa. É um lugar muito misterioso.";
	next;
	mes "[Tripulante]";
	mes "Nós recentemente encontramos uma nova rota no oceano para chegar lá facilmente.";
	if (VIP_SCRIPT) {
		set .@cost, vip_status(VIP_STATUS_ACTIVE)?1000:10000;
		mes "É apenas 10.000 zeny para uma viagem de ida e volta, e 1.000 para VIP! Então você quer ir?";//tradução personalizada (VIP)
	} else {
		set .@cost,10000;
		mes "É apenas 10.000 zeny para uma viagem de ida e volta! Então você quer ir?";
	}
	next;
	switch(select("Leve-me para Brasilis!:Não! Eu ficarei aqui.")) {
	case 1:
		if (Zeny < .@cost) {
			mes "[Tripulante]";
			mes "Eu disse "+callfunc("F_InsertComma",.@cost)+" zeny.";
			close;
		}
		else {
			mes "[Tripulante]";
			mes "Legaaal~!! Vamoooss~!";
			set Zeny, Zeny-.@cost;
			close2;
			warp "brasilis",314,60;
			end;
		}
	case 2:
		mes "[Tripulante]";
		mes "Bem, se você estiver interessado, me avise que eu te levarei até lá.";
		close;
	}
}

brasilis,316,57,3	script	Tripulante#bra1	100,{
	mes "[Tripulante]";
	mes "Meu navio vai voltar para Alberta, você quer se juntar a nós?";
	next;
	switch(select("Voltar para Alberta.:Ainda não~.")) {
	case 1:
		mes "[Tripulante]";
		mes "Com certeza, sinto falta de casa.";
		close2;
		if (checkre(0))
			warp "alberta",243,82;
		else
			warp "alberta",244,115;
		end;
	case 2:
		mes "[Tripulante]";
		mes "Ok, fique à vontade. Nos vemos quando voltarmos então.";
		close;
	}
}

// Generic Brasilis NPCs :: brazil_tre
//============================================================
brasilis,155,165,3	script	Signpost#bra1	858,{
	mes ":: Museu de Arte ::";
	close;
}

brasilis,195,231,3	script	Signpost#bra2	858,{
	mes ":: Monumento às Bandeiras ::";
	close;
}

brasilis,240,247,3	script	Signpost#bra3	858,{
	mes ":: Mercado ::";
	mes " ";
	mes "- Para as suas poções e armamento -";
	close;
}

brasilis,303,309,3	script	Signpost#bra4	858,{
	mes ":: Cabo da selva ::";
	mes "- Não é para os corações fracos -";
	close;
}

brasilis,278,137,3	script	Signpost#bra5	858,{
	mes ":: Brasilis Hotel ::";
	close;
}

brasilis,137,77,5	script	Sorveteiro	85,{
	mes "[Sorveteiro]";
	mes "Venha~Venha~";
	mes "Sorvete é o lanche perfeito para um dia quente~";
	mes "É apenas ^3355FF100 Zeny^000000~";
	mes "Sorvete~";
	mes "Pegue seu sorvete!";
	next;
	switch(select("Me dê um!:Sorvete?:Cancelar.")) {
	case 1:
		mes "[Sorveteiro]";
		mes "Como tem muitas pessoas querendo um sorvete geladinho, você pode encomendar apenas 5 de cada vez.";
		mes "Então, quantos você quer?";
		next;
		while(1) {
			input .@input; //,1,5;
			if (.@input == 0) {
				mes "[Sorveteiro]";
				mes "Nenhum?";
				mes "Bem, saia do caminho, tenho clientes para servir.";
				close;
			}
			else if ((.@input < 0) || (.@input > 5)) {
				mes "[Sorveteiro]";
				mes "Wow.";
				mes "Você pediu muito.";
				mes "Se você comer mais de 5, vai precisar lutar com um monstro no seu estômago hahahaha. Fique calmo, camarada.";
				next;
			}
			else
				break;
		}
		set .@icecream_hap,.@input*100;
		if (Zeny < .@icecream_hap) {
			mes "[Sorveteiro]";
			mes "Poxa~! Você não tem dinheiro suficiente.";
			mes "É apenas ^3355FF100 Zeny^000000~ ...Sinceramente!";
			close;
		}
		if (!checkweight(536,.@input)) {
			mes "[Sorveteiro]";
			mes "Você está carregando muitas coisas.";
			mes "Livre-se um pouco de tanto peso antes de comprar.";
			close;
		}
		set Zeny, Zeny-.@icecream_hap;
		getitem 536,.@input; //Ice_Cream
		close;
	case 2:
		mes "[Sorveteiro]";
		mes "'Sorvete é...";
		mes "Espere, você não sabe ";
		mes "oque é um sorvete?";
		mes "Em que caverna ";
		mes "você tem vivido?";
		next;
		mes "[Sorveteiro]";
		mes "Eu não vou nem começar com o quão estranho isso soa.";
		mes "De qualquer forma, pegue o seu sorvete aqui, enquanto está gostoso e geladinho.";
		close;
	case 3:
		mes "[Sorveteiro]";
		mes "Não perca a chance de comer o melhor sorvete de todo o planeta terra~!";
		close;
	}
}
