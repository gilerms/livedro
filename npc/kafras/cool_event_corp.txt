//===== rAthena Script ======================================= 
//= Corp. Eventos Incríveis
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Corp. Eventos Incríveis (Save, Storage & Pushcart Service)
//= in Arunafeltz Region.
//===== Additional Comments: ================================= 
//= 1.0 First version, iRO Sakray X.4 [Gepard]
//= 1.1 Optimized, fixed a conditional bug [Lupus]
//= 1.1a Redo back. It wasn't a bug [Lupus]
//= 1.2 Added Rachel Corp. Eventos Incríveis [Playtester]
//= 1.3 Added cutins, closed Kafra Password exploit
//=     until we got a client/packet based password support [Lupus]
//= 1.4 Added Veins Corp. Eventos Incríveis. [L0ne_W0lf]
//= 1.5 Cool Event Corp staff is now more iRO like. [L0ne_W0lf]
//= 1.6 Wrote a basic function and added VIP features. [Euphy]
//============================================================ 

//callfunc "F_CoolEventCorp","<menu>","<location>","<save map>",<x>,<y>,"<warp map>";
function	script	F_CoolEventCorp	{
	cutin "zonda_01",2;
	mes "[Corp. Eventos Incríveis]";
	mes "Bem-vindo a Corporação Eventos Incríveis.";
	mes "Nossa equipe está sempre trabalhando";
	mes "para superar suas expectativas";
	mes "a respeito da qualidade de serviço. Então";
	mes "como posso ajudá-lo hoje?";
	next;
	switch(select(getarg(0))) {
	case 1:
		mes "[Corp. Eventos Incríveis]";
		mes "Seu ponto de retorno";
		mes "foi salvo aqui.";
		mes getarg(1)+".";
		mes "Obrigado por usar o serviço";
		mes "da Corp. Eventos Incríveis~";
		savepoint getarg(2),getarg(3),getarg(4),1,1;
		close2;
		break;
	case 2:
		mes "[Corp. Eventos Incríveis]";
		if(!callfunc("F_CanOpenStorage")){
			mes "Me desculpe, mas você";
			mes precisa ser um Aprendiz";
			mes "com habilidades básicas nivel 6";
			mes "para usar o serviço de armazém.";
			close2;
			break;
		}
		if(Zeny < 40){
			mes "Me desculpe, mas você não tem";
			mes "Zeny suficiente para usare";
			mes "o serviço de armazém. Nosso";
			mes "serviço de armazém custa 40 Zeny.";
			close2;
			break;
		}
		set Zeny, Zeny - 40;
		mes "Deixe-me abrir seu armazém";
		mes "pessoal para você imediatamente.";
		mes "Obrigado por apoiar a";
		mes "Corporação de eventos incríveis. por usar";
		mes "nossos serviços. Tenha um bom dia~";
		callfunc("F_CheckKafCode");	//check your storage password, if set
		close2;
		openstorage;
		break;
	case 3:
		mes "Por favor, escolha";
		mes "seu destino.";
		next;
		if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE))
			set .@cost,4400;
		else
			set .@cost,2200;
		.@destination$ = getarg(5);
		if (select(.@destination$ +" -> "+ .@cost +" z:Cancel") == 1) {
			if ((countitem(7060) != 0) || (Zeny >= .@cost)) {
				if (countitem(7060) != 0) {
					delitem 7060,1; //Warp_Free_Ticket
				}
				else {
					set Zeny,Zeny-.@cost;
				}
				if (.@destination$ == "Veins") warp "veins",205,101;
				else if (.@destination$ == "Rachel") warp "rachel",115,125;
				cutin "",255;
				end;
			}
			else {
				mes "[Corp. Eventos Incríveis]";
				mes "Me desculpe, mas você não tem";
				mes "Zeny suficiente para usar o serviço";
				mes "de teletransporte. A taxa para se teletransportar";
				mes "para "+ .@destination$ +" é de "+.@cost+" Zeny.";
			}
		}
		close2;
		break;
	case 4:
		mes "[Corp. Eventos Incríveis]";
		if(BaseClass != Job_Merchant){
			mes "Me desculpe, mas o serviço ";
			mes "de aluguel de carrinho de mão";
			if (checkre(0))
				mes "está disponível apenas para a classe Mercador.";
			else {
				mes "está disponível para mercadores,";
				mes "Ferreiros, Mestre Ferreiro,";
				mes "Alquimista e Criador.";
			}
			close2;
			break;
		}
		if(getskilllv("MC_PUSHCART") == 0){
			mes "Você só pode alugar um carrinho";
			mes "depois de aprender a habilidade Carrihno.";
			close2;
			break;
		}
		if(checkcart() == 1){
			mes "Você já possui um";
			mes "carrinho equipado.";
			mes "Infelizmente, nós não podemos";
			mes "alugar mais de um";
			mes "carrinho por vez.";
			close2;
			break;
		}
		if(countitem(7061)){
			delitem 7061,1; //Cart_Free_Ticket
			setcart;
			close2;
			break;
		}
		mes "O aluguel do carrinho de mão";
		mes "custará 800 Zeny. Você";
		mes "deseja alugar um carrinho de mão?";
		next;
		if(select("Alugar Carrinho.:Cancelar") == 1){
			if(Zeny < 800){
				mes "[Corp. Eventos Incríveis]";
				mes "Lamento, mas você";
				mes "não possui Zeny";
				mes "suficientes para alugar um carrinho.";
				mes "O aluguel custa 800 Zeny.";
			} else {
				set Zeny, Zeny - 800;
				setcart;
			}
		}
		close2;
		break;
	case 5:
		callfunc("F_SetKafCode","[Corp. Eventos Incríveis]","Cool Event Corp.");
		//the function closes msg and removes cutins
	case 6:
	default:
		mes "[Corp. Eventos Incríveis]";
		mes "Corporação de Eventos Incríveis. está sempre";
		mes "se esforçando para oferecer os melhores";
		mes "serviços para nossos clientes.";
		mes "Ajude-nos a ser melhor,";
		mes "fornecendo-nos suas opiniões";
		mes "e feedback sincero. Obrigado.";
		close2;
	}
	cutin "", 255;
	end;
}

hugel,88,168,4	script	Corp. Eventos Incríveis::CoolEventCorpStaffHugel	874,{
	callfunc "F_CoolEventCorp",
		"Salvar:Usar Armazém::Alugar Carrinho:Senha do Armazém:Cancelar",
		"na vila de Hugel","hugel",96,155;
	end;
}

rachel,109,138,5	script	Corp. Eventos Incríveis::CoolEventCorpStaffRachel	874,{
	callfunc "F_CoolEventCorp",
		"Salvar:Usar Armazém:Serviço de teletransporte:Alugar Carrinho:Senha do Armazém:Cancelar",
		"na cidade de Rachel","rachel",113,137,"Veins";
	end;
}

veins,208,128,5	script	Corp. Eventos Incríveis::CoolEventCorpStaffVeins	874,{
	callfunc "F_CoolEventCorp",
		"Salvar:Usar Armazém:Serviço de teletransporte:Alugar Carrinho:Senha do Armazém:Cancelar",
		"na cidade de Veins","veins",204,103,"Rachel";
	end;
}
